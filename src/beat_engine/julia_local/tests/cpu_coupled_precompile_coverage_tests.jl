# Guards the compiled bundles' coverage of the CPU coupled path with the beat_cpu defaults (flux
# elimination, CHOLMOD interface mass, Float32 FEM, refined dense LU). Runs the packaged coupled
# request with CPU BEM assembly and no BLAB_COUPLED_* overrides through the worker entry
# (`coupled_solver.jl`) in a fresh process under `--trace-compile`, and fails when coupled-path
# compilation exceeds a budget. A second run through the source fallback must exceed it. Runs under
# julia_local (CPU bundle) or julia_metal (Metal bundle, which also serves CPU requests).
using Test, JSON
if basename(dirname(Base.active_project())) == "julia_metal"
    import BeatEngineCompiledMetalBundle
    const Bundle = BeatEngineCompiledMetalBundle
    const BUNDLE_ENV = ("BLAB_BEAT_ENGINE_GPU_BACKEND" => "metal",)
else
    import BeatEngineCompiledCpuBundle
    const Bundle = BeatEngineCompiledCpuBundle
    const BUNDLE_ENV = ()
end

const CPU_COUPLED_COMPILE_BUDGET_MS = parse(Float64, get(ENV, "BLAB_CPU_COUPLED_COMPILE_BUDGET_MS", "1500"))
const CPU_COUPLED_PATTERNS = ("BeatEngineCoupledCondensed.", "burton_miller_neumann_matrices")

function cpu_coupled_compile_trace(directory, tag; bundle::Bool)
    request = Bundle.coupled_workload_request(; bem_backend="cpu")
    request_path = joinpath(directory, "request-$tag.json")
    write(request_path, JSON.json(request))
    trace = joinpath(directory, "trace-$tag.jl")
    project = dirname(Base.active_project())
    entry = normpath(joinpath(@__DIR__, "..", "coupled_solver.jl"))
    command = `$(Base.julia_cmd()) --threads=2 --startup-file=no --project=$project --trace-compile=$trace --trace-compile-timing $entry`
    # The production defaults, not an installing or calling environment's overrides.
    cleared = [name => nothing for name in keys(ENV)
               if startswith(name, "BLAB_COUPLED_") || startswith(name, "BLAB_MUMPS_")]
    output = read(pipeline(addenv(command, cleared..., BUNDLE_ENV...,
            "BLAB_BEAT_ENGINE_BUNDLE" => bundle ? "1" : "0"); stdin=request_path), String)
    results = count(line -> startswith(line, "{") && occursin("\"freq_hz\"", line), split(output, '\n'))
    results == 2 || error("coupled request returned $results of 2 results ($tag)")
    all_statements = 0
    offenders = Tuple{Float64,String}[]
    for line in eachline(trace)
        m = match(r"^#=\s*([\d.]+)\s*ms\s*=#\s*precompile\((.*)\)\s*$", strip(line))
        m === nothing && continue
        all_statements += 1
        any(pattern -> occursin(pattern, m[2]), CPU_COUPLED_PATTERNS) || continue
        push!(offenders, (parse(Float64, m[1]), m[2]))
    end
    sort!(offenders; rev=true)
    return (all_statements=all_statements, offenders=offenders, total=sum(first, offenders; init=0.0))
end

@testset "CPU coupled first request with beat_cpu defaults compiles within budget" begin
    mktempdir() do directory
        cached = cpu_coupled_compile_trace(directory, "cached"; bundle=true)
        for (ms, statement) in cached.offenders[1:min(end, 10)]
            println(round(Int, ms), " ms  ", first(statement, 200))
        end
        println("CPU coupled first-request compilation: ", round(Int, cached.total), " ms in ",
            length(cached.offenders), " of ", cached.all_statements, " traced statements (budget ",
            round(Int, CPU_COUPLED_COMPILE_BUDGET_MS), " ms)")
        @test cached.all_statements > 0
        @test cached.total <= CPU_COUPLED_COMPILE_BUDGET_MS
        uncached = cpu_coupled_compile_trace(directory, "source"; bundle=false)
        println("control through the source fallback: ", round(Int, uncached.total), " ms in ",
            length(uncached.offenders), " coupled statements")
        @test uncached.total > CPU_COUPLED_COMPILE_BUDGET_MS
    end
end
